DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;
SOFT-DELIMITERS = "<,>" ;
SUBREADINGS = LTR ; 
#SUBREADINGS = RTL ; 

LIST N = n ; 
LIST V = vblex ; 
LIST A = adj ; 
LIST Adv = adv ; 
LIST Post = post ; 
LIST Pron = prn ; 
LIST Det = det ;

LIST Nom = nom ; 
LIST Erg = erg ; 
LIST Gen = gen ; 
LIST Dat = dat ; 
LIST Loc = loc ; 

LIST Masc = m ; 
LIST Fem = f ; 
LIST Neut = nt ; 
LIST Mfn = mfn ; 
LIST Pl = pl ; 

LIST IV = iv "букӀин" ; 
LIST TV = tv ; 

LIST Top = top ; 
LIST Cog = cog ; 
LIST Ant = ant ; 
LIST Place = "шагьар" "район" ; 

LIST Attr = attr ;

LIST Class = m f nt pl ; 
LIST Case = nom erg gen dat ; 

SECTION

SELECT Attr IF (0 Top) (1 Place) ; 

SELECT Attr IF (0 Ant) (1 Cog) ; 

SELECT A IF (0C A OR N) (1C N) ;

SELECT N IF (0C V OR N) (1C V) ;

REMOVE Post IF (0C N OR Post) (-1 V) ;   
    ## Дун вукIине вуго ахихъан.

SELECT Det IF (0C Det OR Pron) (0C $$Case) (1C N + $$Case) ; 
    ## Кинаб батIалъи бугеб реализмалда ва романтизмалда гьоркьоб?

REMOVE Pron + Nom IF (-1C Pron + Nom) (0C Det OR Pron) ;
    ## Гьадинаб чIухIигун ана дов гьаб ракьалдаса.

REMOVE Pron + Nom IF (-1* Pron + Nom) (0C Det OR Pron) ; 
    ## ГьанжелъизегӀан гьев хӀалтӀулев вукӀун вуго гьеб гӀуцӀиялъул бетӀерав бухгалтерлъун.

REMOVE Det IF (NOT 1 N OR A) ;
    ## ГьанжелъизегӀан гьев хӀалтӀулев вукӀун вуго гьеб гӀуцӀиялъул бетӀерав бухгалтерлъун.

SELECT Post IF (-1C Loc) (0C Adv OR Post); 

SECTION

LIST @SUBJ→ = @SUBJ→ ; 
LIST @←SUBJ = @←SUBJ ; 

MAP @SUBJ→ TARGET Nom IF (1C IV) (0 $$Class) (1 $$Class) ;
MAP @SUBJ→ TARGET Nom IF (1C IV) (0 Masc OR Fem OR Neut) (1 Mfn);
MAP @SUBJ→ TARGET Erg IF (1C TV)  (0 $$Class) (1 $$Class);
MAP @SUBJ→ TARGET Erg IF (1C TV) (0 Masc OR Fem OR Neut) (1 Mfn);

MAP @←SUBJ TARGET Nom IF (-1C IV)  (0 $$Class) (-1 $$Class);
MAP @←SUBJ TARGET Nom IF (-1C IV)  (0 Masc OR Fem OR Neut) (-1 Mfn);
MAP @←SUBJ TARGET Erg IF (-1C TV)  (0 $$Class) (-1 $$Class);
MAP @←SUBJ TARGET Erg IF (-1C TV)  (0 Masc OR Fem OR Neut) (-1 Mfn);
