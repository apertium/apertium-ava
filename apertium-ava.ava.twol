Alphabet

  а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я
  А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я

  %{u%}:у
  %{a%}:а 
  %{a²%}:а
  %{a³%}:а
  %{и%}:и
  %{й%}:0
  %{ъ%}:0

  %+
  % 
  %>:0 ;

Sets

Vow = а е ё и о у ы э ю я ;

VowRight = е ё ю я ;

Rules

"Yotise {a} after vowel"
%{a%}:я <=> :Vow %>: _ ;

"Delete {u} after stem final vowel"
%{u%}:0 <=> :Vow %>: _ ; 

"Surface {ъ}"
%{ъ%}:ъ <=> _ %>: %{u%}: ;

"Delete {a³} after stem final vowel"
%{a³%}:0 <=> :Vx %>: _ ; 
     where Vx in ( а и у о е ю ) ;

"Delete stem final vowel before {a²}"
Vx:0 <=> _ %>: %{a²%}: ; 
     where Vx in ( а и у о е ю ) ;

! Форма вопросительного наклонения образуется в том случае, если в предложении нет вопросительных слов.
! Образуется она от любой глагольной формы прибавлением частицы -иш к соответсвующей глагольной 
!    форме (при этом звуки -а, -о в окончании глагольной формы перед -иш выпадают).
"Final vowel"
Vx:0 <=> _ %>: %{и%}: ; 
     where Vx in ( а о ю ) ;

"J insertion"
%{й%}:й <=> :Vx %>: _ ; 
     where Vx in ( а и у о е ю ) ;
